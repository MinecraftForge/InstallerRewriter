import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
}

group = 'net.minecraftforge'
version = gitversion.tagOffset

java.toolchain.languageVersion = JavaLanguageVersion.of 8

license {
    header = rootProject.file('LICENSE-header.txt')
    newLine false
    include '**/*.java'
}

dependencies {
    implementation libs.jopt

    implementation libs.log4j

    implementation libs.bundles.maven
    implementation libs.commons.lang
    implementation libs.guava
    implementation libs.gson
    implementation libs.quack

    implementation libs.okhttp

    compileOnly libs.nulls
}

tasks.named('jar', Jar) {
    enabled = false
}

tasks.named('shadowJar', ShadowJar) {
    manifest {
        attributes 'Main-Class': 'net.minecraftforge.ir.InstallerRewriter'
        attributes 'Multi-Release': 'true'
    }

    archiveClassifier = ''
    transform(Log4j2PluginsCacheFileTransformer) { }
    from rootProject.file('LICENSE.txt')
}

tasks.named('assemble') {
    dependsOn tasks.named('shadowJar', ShadowJar)
}

idea.module { downloadSources = downloadJavadoc = true }
