buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://maven.minecraftforge.net/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradleutils:GradleUtils:1.0.+'
    }
}

plugins {
    id 'java'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'net.minecraftforge.gradleutils'

group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

license {
    header project.file('LICENSE-header.txt')
    include 'net/minecraftforge/ir/**/*.java'
    newLine false
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.minecraftforge.net/' }
    maven { url 'https://maven.covers1624.net/' }
}

configurations {
    shadow
    api.extendsFrom shadow
}

dependencies {
    shadow 'net.sf.jopt-simple:jopt-simple:5.0.4'

    shadow 'org.apache.logging.log4j:log4j-core:2.14.1'

    shadow 'org.apache.maven:maven-repository-metadata:3.8.1'
    shadow 'org.apache.maven:maven-artifact:3.8.1'
    shadow 'org.apache.commons:commons-lang3:3.12.0'
    shadow 'com.google.guava:guava:31.0.1-jre'
    shadow 'com.google.code.gson:gson:2.8.8'
    shadow 'net.covers1624:Quack:0.3.7.29'

    shadow 'com.squareup.okhttp3:okhttp:4.9.1'
}

jar.enabled = false
shadowJar {
    build.dependsOn it
    configurations = [project.configurations.shadow]
    classifier ''
    manifest {
        attributes 'Main-Class': 'net.minecraftforge.ir.InstallerRewriter'
        attributes 'Multi-Release': 'true'
    }

    exclude 'module-info.class'
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
    from file("LICENSE.txt")
}
